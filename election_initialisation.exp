#!/usr/bin/expect --

#
# Trusted and Transparent Voting Systems: Verify My Vote Demonstrator
#
# (c) University of Surrey 2019
#

#
# Election initialisation script.
#
# Creates a sub-directory with the name of the election to contain all of the election files and then executes the
# election initialisation.
#
# Requirements:
# - "expect" installed
# - chmod +x election_initialisation.exp election_common.exp
# - teller 1 is assumed to be an ERS computer
# - Verificatum and Java installed
#

# Include common script.
source election_common.exp

# Define values from the command line.
if {[llength $argv] < 12} {
  send_user "Usage: election_initialisation.exp vmv_far_file ssh_key_file sftp_host sftp_user \"election_name\" number_of_tellers threshold_tellers teller teller_ip teller_main_port teller_hint_port \[number_of_voters\]\n"
  exit 1
}

set home_dir [lindex $argv 0]
set jar [lindex $argv 1]
set ssh_key_file [lindex $argv 2]
set sftp_host [lindex $argv 3]
set sftp_user [lindex $argv 4]
set election_name [lindex $argv 5]
set number_of_tellers [lindex $argv 6]
set threshold_tellers [lindex $argv 7]
set teller [lindex $argv 8]
set teller_ip [lindex $argv 9]
set teller_main_port [lindex $argv 10]
set teller_hint_port [lindex $argv 11]

if {$teller == 1} {
  if {[llength $argv] < 13} {
    send_user "Usage: election_initialisation.exp vmv_far_file ssh_key_file sftp_host sftp_user \"election_name\" number_of_tellers threshold_tellers teller teller_ip teller_main_port teller_hint_port number_of_voters\n"
    exit 1
  }

  set number_of_voters [lindex $argv 12]
}

# start.
send_user "election: $election_name\n"
send_user "tellers: $number_of_tellers with a threshold of $threshold_tellers\n"
send_user "teller: $teller $teller_ip $teller_main_port $teller_hint_port\n"

if {$teller == 1} {
  send_user "voters: $number_of_voters\n"
}

# create the election directory.
#exec mkdir "$election_name"
cd "$home_dir/elections/$election_name"

# if this is teller 1, create the election parameters.
if {$teller == 1} {
  send_user -- "- create election parameters ----------------------------------------------------------------------------------\n"
  execute_vmv "create-election-parameters --publish public-election-params.csv --name \"$election_name\" --number-of-tellers $number_of_tellers --threshold-tellers $threshold_tellers\r"
  upload "public-election-params.csv" 1
}

# create the teller.
send_user -- "- create teller -----------------------------------------------------------------------------------------------\n"
download "public-election-params.csv"
execute_vmv "create-teller --election public-election-params.csv --teller $teller --ip $teller_ip --teller-port $teller_main_port --hint-port $teller_hint_port --publish teller-information-$teller.xml\r"
upload "teller-information-$teller.xml" 0

# merge tellers.
send_user -- "- merge tellers -----------------------------------------------------------------------------------------------\n"
set index 1
set files ""
while {$index <= $number_of_tellers} {
  set file "teller-information-$index.xml"
  append files "$file "
  download $file
  set index [expr $index+1]
}
execute_vmv "merge-teller --election public-election-params.csv --teller-information $files --teller $teller\r"

# Create election keys.
send_user -- "- Create Election Keys ----------------------------------------------------------------------------------------\n"
execute_vmv "create-election-keys --election public-election-params.csv --output election-keys-$teller.csv --publish public-election-keys-$teller.csv --teller $teller\r"
upload "election-keys-$teller.csv" 0
upload "public-election-keys-$teller.csv" 0

# If this is teller 1, create the voters' tracker numbers.
if {$teller == 1} {
  send_user -- "- Create Tracker Numbers ------------------------------------------------------------------------------------\n"
  execute_vmv "create-tracker-numbers --election public-election-params.csv public-election-keys-$teller.csv --number-of-voters $number_of_voters --publish public-tracker-numbers.csv\r"
  upload "public-tracker-numbers.csv" 0
}

# Shuffle the tracker numbers.
send_user -- "- Shuffle Tracker Numbers -------------------------------------------------------------------------------------\n"
download "public-tracker-numbers.csv"
execute_vmv "shuffle-tracker-numbers --election public-election-params.csv --tracker-numbers public-tracker-numbers.csv --publish shuffled-tracker-numbers-$teller.csv shuffle-proofs-$teller.zip --teller $teller\r"
upload "shuffled-tracker-numbers-$teller.csv" 0
upload "shuffle-proofs-$teller.zip" 0

# Map vote options.
if {$teller == 1} {
  send_user -- "- Map Vote Options --------------------------------------------------------------------------------------------\n"
  execute_vmv "map-vote-options --election public-election-params.csv --vote-options ers-vote-options.csv --publish public-vote-options.csv\r"
  upload "ers-vote-options.csv" 0
  upload "public-vote-options.csv" 0
}

download "ers-vote-options.csv"
download "public-vote-options.csv"
